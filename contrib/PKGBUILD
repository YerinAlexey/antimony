# Maintainer: Garrit Franke <garrit@slashdev.space>
# Contributor: Alexey Yerin <yyp@disroot.org>
pkgname="sabre-git"
pkgver=v0.4.0.r7.fe7b035
pkgrel=1
pkgdesc="The bullshit-free programming language"
url="https://github.com/garritfra/sabre"
license=("Apache")
arch=("x86_64" "pentium4" "i686")
makedepends=("git" "cargo")
optdepends=(
	"node: Node.js backend"
	"llvm: LLVM backend"
)
provides=("sabre")
source=(
	"sabre::git+https://github.com/garritfra/sabre"
)
sha256sums=("SKIP")

pkgver() {
	cd "${srcdir}/sabre"

	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "${srcdir}/sabre"

	cargo build --release
}

check() {
	cd "${srcdir}/sabre"

	cargo test
}

package() {
	cd "${srcdir}/sabre"

	install -Dm755 target/release/sabre "${pkgdir}/usr/bin/sabre"
}
